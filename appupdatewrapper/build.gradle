apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.novoda.bintray-release'
apply plugin: 'org.jetbrains.dokka-android'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:${dokka_version}"
    }
}

repositories {
    google()
    mavenCentral()
}

kapt {
    mapDiagnosticLocations = true
    useBuildCache = true
}

android {
    compileSdkVersion androidCompileSdkVersion
    buildToolsVersion androidBuildToolsVersion
    testOptions.unitTests.includeAndroidResources = true

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    defaultConfig {
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionCode versionCode
        versionName versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath false
            }
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    api 'androidx.core:core:1.2.0-alpha04'
    kapt 'androidx.lifecycle:lifecycle-common-java8:2.2.0-alpha05'
    api 'com.google.android.play:core:1.6.3'

    implementation 'com.jakewharton.timber:timber:4.7.1'

    testImplementation project(":testapp")
    testImplementation 'androidx.test:core:1.2.1-alpha02'
    testImplementation 'androidx.test.ext:junit:1.1.2-alpha02'
    testImplementation 'junit:junit:4.13-beta-3'
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    testImplementation 'org.robolectric:robolectric:4.3'
}

dokka {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task javadocJar(type: Jar, dependsOn: dokka) {
    archiveClassifier.set('javadoc')
    from "$buildDir/javadoc"
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.source
}

artifacts {
    archives sourcesJar, javadocJar
}

publish {
    userOrg = bintrayUserOrg
    repoName = bintrayRepoName
    groupId = rootProject.group
    artifactId = 'appupdatewrapper'
    publishVersion = rootProject.version
    licences = ["Apache-2.0"]
    desc = "A wrapper for Android `AppUpdateManager` to encapsulate update workflow."
    website = "https://github.com/motorro/AppUpdateWrapper"
}

